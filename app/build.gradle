apply plugin: "com.android.application"

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.tachibana.downloader"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 7
        /*
         * Convection:
         *  major.minor.[revision][-some_info]
         */
        versionName "1.1.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    testOptions.unitTests.includeAndroidResources = true

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    lintOptions {
        abortOnError false
    }
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams true
    }
}

def ACRA_VERSION = "5.5.0"
def ROOM_VERSION = "2.2.5"

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    /* Testing */
    testImplementation "junit:junit:4.13"
    testImplementation "org.robolectric:robolectric:4.3.1"
    androidTestImplementation "androidx.test.ext:junit:1.1.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.2.0"
    androidTestImplementation "androidx.test:rules:1.2.0"
    androidTestImplementation "androidx.room:room-testing:$ROOM_VERSION"
    testAnnotationProcessor 'com.google.auto.service:auto-service:1.0-rc7'

    /* AndroidX libraries */
    implementation "androidx.appcompat:appcompat:1.2.0-rc01"
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-beta6"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.recyclerview:recyclerview-selection:1.1.0-rc01"
    implementation "com.google.android.material:material:1.2.0-alpha06"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0-rc01"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.room:room-runtime:$ROOM_VERSION"
    implementation "androidx.room:room-rxjava2:$ROOM_VERSION"
    annotationProcessor "androidx.room:room-compiler:$ROOM_VERSION" /* For schemas import */
    implementation "androidx.work:work-runtime:2.3.4"

    /* ReactiveX */
    implementation "io.reactivex.rxjava2:rxjava:2.2.9"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"

    /* ACRA */
    implementation "ch.acra:acra-mail:$ACRA_VERSION"
    implementation "ch.acra:acra-dialog:$ACRA_VERSION"

    /* Other */
    implementation "net.cachapa.expandablelayout:expandablelayout:2.9.2"
    implementation "com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:1.0.0"
    implementation "com.takisoft.preferencex:preferencex:1.1.0"
    implementation "com.jaredrummler:colorpicker:1.1.0"
    implementation "com.github.anthonynsimon:jurl:v0.4.2"
    implementation "net.yslibrary.keyboardvisibilityevent:keyboardvisibilityevent:3.0.0-RC2"
}
