apply plugin: 'com.android.application'

android {
    compileSdkVersion 31
    defaultConfig {
        applicationId 'com.tachibana.downloader'
        minSdkVersion 19
        targetSdkVersion 31
        versionCode 15
        /*
         * Convection:
         *  major.minor.[revision][-some_info]
         */
        versionName '1.6.2'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ['room.schemaLocation': "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        dataBinding true
    }

    testOptions.unitTests.includeAndroidResources = true

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    lint {
        abortOnError false
    }
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat 'full'
        events 'started', 'skipped', 'passed', 'failed'
        showStandardStreams true
    }
}

def ACRA_VERSION = '5.8.4'
def ROOM_VERSION = '2.4.2'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    /* Testing */
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.7.3'
    testImplementation('org.mockito:mockito-core:4.4.0')
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation "androidx.room:room-testing:$ROOM_VERSION"
    testAnnotationProcessor 'com.google.auto.service:auto-service:1.0.1'

    /* AndroidX libraries */
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'
    implementation 'com.google.android.material:material:1.6.0-alpha03'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.room:room-runtime:$ROOM_VERSION"
    implementation "androidx.room:room-rxjava2:$ROOM_VERSION"
    annotationProcessor "androidx.room:room-compiler:$ROOM_VERSION" /* For schemas import */
    implementation 'androidx.work:work-runtime:2.7.1'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation "androidx.preference:preference:1.2.0"

    /* ReactiveX */
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    /* ACRA */
    implementation "ch.acra:acra-mail:$ACRA_VERSION"
    implementation "ch.acra:acra-dialog:$ACRA_VERSION"

    /* Other */
    implementation 'com.github.cachapa:ExpandableLayout:2.9.2'
    implementation 'com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:1.0.0'
    implementation 'com.jaredrummler:colorpicker:1.1.0'
    implementation 'com.github.anthonynsimon:jurl:v0.4.2'
    implementation 'net.yslibrary.keyboardvisibilityevent:keyboardvisibilityevent:3.0.0-RC3'
    implementation 'com.github.albfernandez:juniversalchardet:2.4.0'
    implementation 'org.apache.commons:commons-compress:1.21'
    implementation 'org.tukaani:xz:1.9'
}
